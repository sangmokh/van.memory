result-view {
  match: RecallResponse (this) {
  }

  message {
    if (size (this.memories) == 0) {
      template ("#{value(this.speech)}")
    } else-if (size (this.memories) == 1) {
      template ("Here's the best match.") {
        speech ("#{value(this.speech)}")
      }
    } else {
      template ("Here are similar memories.") {
        speech ("#{value(this.speech)} Here are all memories that sound similar:")
      }
    }
  }

  render {
    list-of (this.memories) {
      navigation-mode {
        if ($handsFree) {
          read-one-and-next {
            page-content {
              underflow-statement (This is the first memory.)
              overflow-statement (Those are all the memories.)
              next-item-question (Would you like to hear the next matching memory?)
            }
          }
        }
      }
      where-each (item) {
        spoken-summary {
          template ("You told me #{value(item.howLongAgo)}, #{value(item.text)}")
        }
        layout-macro (memory-summary) {
          param (memory) {
            expression (item)
          }
        }
      }
    }
  }

  conversation-drivers {
    conversation-driver {
      template ("List all memories")
    }
    if (size (this.memories) == 1) {
      conversation-driver {
        template ("Delete this memory")
      }
    } else-if (size (this.memories) > 1) {
      conversation-driver {
        template ("Delete these memories")
      }
    }
  }
}
