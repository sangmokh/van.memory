result-view {
  match: ListResponse (this) {
    min (Required) max (One)
  }

  message {
    template ("#{value(this.speech)}")
  }

  render {
    list-of (this.memories) {
      navigation-mode {
        if ($handsFree) {
          read-many-and-next {
            page-size (5)
            list-summary ("#{value(this.speech)}")
            page-content (page) {
              page-marker {
                if (size(page) >= 1) {
                  if (size(this.memories) > 5) {
                    if (isFirstNavPage(page)) {
                      template ("The first #{size(page)} memories are:")
                    } else-if (isLastNavPage(page)) {
                      if (size(page) == 1) {
                        template ("The last memory is:")
                      } else {
                        template ("The last #{size(page)} memories are:")
                      }
                    } else {
                      template ("The next #{size(page)} memories are:")
                    }
                  } else-if (size(this.memories) == 1) {
                    template ("This memory is:")
                  } else {
                    template ("These memories are:")
                  }
                }
              }
              underflow-statement (This is the first memory.)
              overflow-statement (Those are all the memories.)
              overflow-question (What would you like to do?)
              next-page-question (Would you like to hear more memories?)
            }
          }
        }
      }
      where-each (item) {
        layout-macro (memory-summary) {
          param (memory) {
            expression (item)
          }
        }
      }
    }
  }

  conversation-drivers {
    if (size (this.memories) > 0) {
      conversation-driver {
        template ("Most recent memory")
      }
      conversation-driver {
        template ("Delete all memories")
      }
    }
  }
}
